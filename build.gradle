plugins {
	id 'org.springframework.boot' version '2.6.6'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'org.hidetake.swagger.generator' version '2.19.2'
}

group = 'io.igorun.demo.openapi'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

swaggerSources {
	def typeMappings = 'URI=URI'
	def importMappings = 'URI=java.net.URI'
	eLibrary {
		def apiYaml = "${rootDir}/src/main/resources/api/openapi.yaml"
		def configJson = "${rootDir}/src/main/resources/api/config.json"
		inputFile = file(apiYaml)
		def ignoreFile = file("${rootDir}/src/main/resources/api/.openapi-generator-ignore")
		code {
			language = 'spring'
			configFile = file(configJson)
			rawOptions = ['--ignore-file-override', ignoreFile, '--type-mappings',
						  typeMappings, '--import-mappings', importMappings] as List<String>
			components = [models: true, apis: true, supportingFiles: 'ApiUtil.java']
			//dependsOn validation // Should be uncommented once plugin starts supporting OA 3 validation
		}
	}
}

compileJava.dependsOn swaggerSources.eLibrary.code
sourceSets.main.java.srcDir "${swaggerSources.eLibrary.code.outputDir}/src/main/java"
sourceSets.main.resources.srcDir "${swaggerSources.eLibrary.code.outputDir}/src/main/resources"

repositories {
	mavenCentral()
}

dependencies {
	swaggerCodegen 'org.openapitools:openapi-generator-cli:4.3.1'
	compileOnly 'io.swagger:swagger-annotations:1.6.5'
	compileOnly 'org.springframework.boot:spring-boot-starter-validation:2.6.6'
	compileOnly 'org.openapitools:jackson-databind-nullable:0.2.2'
	compileOnly 'org.glassfish.jaxb:jaxb-runtime:3.0.2'
	implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.13.2'
	implementation 'org.springframework.boot:spring-boot-starter-hateoas'
	implementation 'org.springframework.boot:spring-boot-starter'
	compileOnly 'org.projectlombok:lombok:1.18.22'
	annotationProcessor 'org.projectlombok:lombok:1.18.22'

	testCompileOnly 'org.projectlombok:lombok:1.18.22'
	testAnnotationProcessor 'org.projectlombok:lombok:1.18.22'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
	useJUnitPlatform()
}
